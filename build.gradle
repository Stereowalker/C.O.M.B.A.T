buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.parchmentmc:librarian:1.+'
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
  id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, modId+".refmap.json"
}

version = modMinecraftVersion + "-" +modVersion
group = modGroup // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = modFilename

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
println('Mod Name: ' + modFilename + ', Mod Version: ' + modVersion + ', Minecraft Version: ' + modMinecraftVersion + ', Forge Version: ' + modForgeVersion)

minecraft {
    mappings channel: mappingsChannel, version: mappingsVersion
    //makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
			arg "-mixin.config="+modId+".mixins.json"

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
			
			args '--username', 'Stereowalker', '--uuid', "$System.env.MINECRAFTUUIDSTEREOWALKER"

            mods {
                modId {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'REGISTRIES'
			arg "-mixin.config="+modId+".mixins.json"
            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                modId {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
	maven {url = "https://dvs1.progwml6.com/files/maven/"} // JEI
	maven {url = "https://maven.theillusivec4.top/"} //Curios, Caelus
	maven {url 'https://maven.blamejared.com'}
	maven {url "https://modmaven.dev/" } //UnionLib
	maven {url "https://www.cursemaven.com"} //cursemaven
    maven {url = "https://repo.spongepowered.org/maven"} // mixin
    maven {url "https://maven.shedaniel.me/"} // cloth config
}

dependencies {
	minecraft 'net.minecraftforge:forge:' + modMinecraftVersion + '-' + modForgeVersion
	annotationProcessor "org.spongepowered:mixin:"+modMixinVersion+":processor"
	implementation "org.spongepowered:mixin:"+modMixinVersion+""
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation fg.deobf("com.stereowalker.unionlib:UnionLib:"+modMinecraftVersion+"-"+unionLibVersion+"-"+modLoader+"")
	implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:"+clothConfigVersion+"")
	//implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${modMinecraftVersion}-${terrablenderVersion}")

	compileOnly fg.deobf("mezz.jei:jei-"+modMinecraftVersion+":"+jeiVersion+":api")
    compileOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:"+oldMinecraftVersion+"-"+caelusVersion+":api") 
	compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:"+modMinecraftVersion+"-"+curiosVersion+":api")
	compileOnly fg.deobf("vazkii.patchouli:Patchouli:"+modMinecraftVersion+"-"+patchouliVersion+":api")

	runtimeOnly fg.deobf("mezz.jei:jei-"+modMinecraftVersion+":"+jeiVersion+"")
    runtimeOnly fg.deobf("top.theillusivec4.caelus:caelus-forge:"+oldMinecraftVersion+"-"+caelusVersion+"") 
	runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:"+modMinecraftVersion+"-"+curiosVersion+"")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:"+modMinecraftVersion+"-"+patchouliVersion+"")
	
	//implementation fg.deobf("curse.maven:biomes-o-plenty-220318:"+bopID)
	//implementation fg.deobf("com.stereowalker.survive:Survive:"+modMinecraftVersion+"-"+surviveVersion+"")
	implementation fg.deobf("curse.maven:origins-474438:"+originsID)

}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": modFilename,
            "Specification-Vendor": "Stereowalker",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Stereowalker",
            "MixinConfigs": modId+".mixins.json",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = "deobf"
}

task apiJar(type: Jar) {
    from(sourceSets.main.allJava) {
        include 'com/stereowalker/combat/api/**'
		include 'com/stereowalker/rankup/api/**'
		include 'com/stereowalker/rankup/skills/api/**'
    }
    from (sourceSets.main.output) {
        include 'com/stereowalker/combat/api/**'
		include 'com/stereowalker/rankup/api/**'
		include 'com/stereowalker/rankup/skills/api/**'
    }
	classifier = 'api'
}

curseforge {
    project {
    	id = modCurseId
		apiKey = "$System.env.CURSEFORGEKEY"
		changelog = file('changelog.md')
		changelogType = "markdown"
		releaseType = 'beta'
		addGameVersion(modMinecraftVersion)
        addGameVersion("Forge")
		relations {
      		requiredDependency 'unionlib'
		}	
		mainArtifact(jar) {
			displayName = modFilename+"-$project.version"
		}
		addArtifact deobfJar
		addArtifact apiJar
		addArtifact sourcesJar
	}
	options {
    	debug = false // defaults to false
  	}		
}

publishing {
    publications {
        mavenJava(MavenPublication) {
        	afterEvaluate {
                artifact project.jar
                artifact project.apiJar
                artifact project.sourcesJar
            }
        
        	groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
        }
    }
    repositories {
        maven {
            name = "modmaven"
			credentials {
				username "$System.env.ARTIFACTORYUSER"
				password "$System.env.ARTIFACTORYPASS"
			}
			url = "https://modmaven.dev/artifactory/local-releases/"
        }
    }
}

artifacts {
	archives sourcesJar
	archives deobfJar
	archives apiJar
}